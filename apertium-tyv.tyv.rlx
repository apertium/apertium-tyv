
###############################################################################

DELIMITERS = "<.>" "<!>" "<?>" "<..>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;
OPTIONS += no-inline-sets no-inline-templates ;

SUBREADINGS = LTR ; # Alternate, left-to-right (main reading on the left)

###############################################################################

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST V = v ;
LIST A = adj ;
LIST N = n ;
LIST Adv = adv ;
LIST Det = det ;
LIST Num = num ;
LIST Pron = prn ; 
LIST Post = post ; 
LIST Cop = cop ; 
LIST Mod = mod ;

LIST Aux = vaux ; 
LIST Qst = qst ;

LIST Subst = subst ;
LIST Attr = attr ; 

LIST Coop = coop ;

LIST Acc = acc ;
LIST Gen = gen ; 
LIST Nom = nom ;
LIST Loc = loc ;
LIST Dat = dat ;
LIST Abl = abl ;
LIST All = all ;

LIST Px1Sg = px1sg ;
LIST Px2Sg = px2sg ;
LIST Px3Sp = px3sp ;
LIST Px1Pl = px1pl ;
LIST Px2Pl = px1pl ;
LIST Px3Pl = px3pl ;

LIST Gpr/Aor = gpr_aor ; 
LIST Gpr/Past = gpr_past ; 

LIST Prc/Aor = prc_aor ;
LIST Prc/Impf = prc_impf ;
LIST Prc/Perf = prc_perf ;

LIST Past = past ; 
LIST Aor = aor ; 
LIST Imper = imp ; 

LIST Ant = ant ; 
LIST Cog = cog ;
LIST Pat = pat ;

LIST CC = cnjcoo ;

LIST Guio = "-" ;
LIST Cm = "," ; 
LIST Ques = "?" ;
LIST Sent = "." ".." "..." ;
LIST Elip = "..." ;
LIST Excl = "!" ;
LIST Quot = lquot rquot quot ;

LIST Title/Word = ("<[А-ЯҢӨҮA-Z][а-яңөүa-z]+>"r) ;
LIST Maj/Word = ("<[А-ЯҢӨҮA-Z]+>"r) ;
LIST Min/Word = ("<[а-яңөүa-z]+>"r) ;

LIST Title/Dict = ("[А-ЯҢӨҮA-Z][а-яңөүa-z]+"r) ;
LIST Maj/Dict = ("[А-ЯҢӨҮA-Z]+"r) ;
LIST Min/Dict = ("[а-яңөүa-z]+"r) ;

###############################################################################

SET Gpr = Gpr/Aor | Gpr/Past ; 

SET Prc = Prc/Aor | Prc/Perf | Prc/Impf ; 

SET Fin = Past | Aor ; 

SET Local = Loc | Dat | Abl | All ;

SET Poss = Px1Sg | Px2Sg | Px3Sp | Px1Pl | Px2Pl | Px3Pl ; 

SET Nominal = Pron | Subst | N ; 

SET Modifier = A - Subst | Det | Num - Subst | Gpr | Gen ;

SET Punct = Guio | Cm | Sent | Ques | Elip | Excl | Quot ;

SET Name = Ant | Cog | Pat ;

###############################################################################

SECTION

REMOVE Gpr IF (1 EOS) ; 
   ## Ол сеңээ бүзүрээр.

SELECT Aux IF (-1C Prc);

REMOVE Aux IF (NOT -1 Prc) ;
   ## Ол кижи биске бо номну берген.

SELECT Qst IF (1 Ques) ;
   ## Олар ол кино көргени ол бе?

REMOVE Attr IF (1C* Fin BARRIER (*) - Prc) ;

REMOVE Attr IF (1C Post) ;

REMOVE Prc (NOT 1 Prc OR Aux)  ;

REMOVE Attr IF (1C Poss) (NOT -1 Gen) ;

REMOVE Attr IF (1 EOS) ;

REMOVE Attr IF (1C Det) ;

REMOVE Attr IF (1 Prc) ; 
   ## Картофельде соланин деп ногаанзымаар өңнүг, хоранныг бүдүмел бар.

REMOVE Poss IF (-1 Cm) ;
   ## Оон аңгыда, кижини ооң хамааржып турары күрүнениң азы девискээрниң политиктиг, хоойлу-дүрүм ...

REMOVE Poss IF (-1 Local) ;

REMOVE Fin IF (0 Fin OR Subst) (1C Local + Poss) ;
   ## Ол ышкаш үүлгедигниң болган үезинде берип болур турган кеземчеден аар кеземче бээри болдунмас.

REMOVE Imper IF (1 Cm) ;
   ## Олар бадыланышканынга хамаарыштыр, бадылалдыг турган үезинде база ону күш чок деп санаан үезинде дең эргелерни ажыглаар.

REMOVE Poss IF (-1 BOS) ;

REMOVE SUB:1 Qst IF (1 Punct) (NOT 1 Ques) ;
   ## 30 чүүл.

SELECT Title/Dict IF (NOT -1 BOS) (0 Title/Word) ;
   ## Абакан биле Кызылдың аразы.

REMOVE SUB:1 Cop IF (NOT 1 EOS) ;

REMOVE Imper IF (NOT 1* Excl) ;

SELECT Attr IF (0C Nom OR Attr) (1 Guio LINK 1 N) ;
   ## 

SELECT SUB:1 Cop IF (1 EOS) (NOT 0 Fin) (NOT -1* Fin) ;
   ## Азамат биле Айгуль садта.

REMOVE Num + Subst IF (1C N + Nom) ;

SELECT Attr IF (0C Ant OR Pat) (1C Cog) ;

SELECT Attr IF (0C Ant) (1C Pat OR Cog) ;

###############################################################################

SECTION

REMOVE Coop IF (0C Coop OR V - Coop) ;

###############################################################################

SECTION 

LIST @cmpnd = @cmpnd ;
LIST @amod = @amod ;
LIST @advmod = @advmod ;
LIST @det = @det ; 
LIST @obj = @obj ;
LIST @case = @case ;
LIST @punct = @punct ;
LIST @cnp  = @cnp ;
LIST @cvp  = @cvp ;
LIST @main = @main ;
LIST @aux = @aux ;
LIST @cop = @cop ;
LIST @disc = @disc ;
LIST @nmod = @nmod ;
LIST @nummod = @nummmod ;
LIST @name = @name ;

MAP @cmpnd TARGET Attr - Name ;

MAP @amod TARGET A - Subst IF (1 A OR N) ;

MAP @det TARGET Det ; 

MAP @obj TARGET Acc ;

MAP @nmod TARGET N + Gen - Subst ;

MAP @nmod TARGET N + Local - Subst ;

MAP @case TARGET Post ; 

MAP @punct TARGET Punct ;

MAP @main TARGET Fin IF (NOT 0 Aux) ; 

MAP @advmod TARGET Adv ; 

MAP @aux TARGET Aux + Fin;

MAP @main TARGET Prc IF (1 @aux) ;

MAP @cop TARGET Cop ; 

MAP SUB:1 @cop TARGET Cop ; 

MAP @disc TARGET Mod ; 

MAP @nummod TARGET Num - Subst ;

MAP @name TARGET Name + Attr ;

###############################################################################

SECTION

SELECT @main IF (0 @main) (1 EOS) ;
