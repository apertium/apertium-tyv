
Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
   а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я

   %{I%}:ы ! %{I%}:и %{I%}:у %{I%}:ү

   %{A%}:а ! %{A%}:е

   %{E%}:а %{A%}:е

   %{D%}:д 
 
   %{L%}:л 

   %{N%}:н 
 
   %{G%}:г ! %{G%}:к

   %{g%}:г
 
   %{J%}:ж %{J%}:ч

   %{B%}:б ! %{B%}:п %{B%}:м %{B%}:в

   %{i%}:0 ! %{i%}:ы %{i%}:и %{i%}:у %{i%}:ү

!   %{z%}:з        %{y%}:ы
           %{n%}:н 
   %{z%}:0 %{n%}:0 %{y%}:0

   %{и%}:и %{и%}:у %{и%}:ы 

	%{y%}:0

   %{к%}:к
   %{г%}:г
   %{н%}:0

   %{э%}:0

   %{☭%}:0

   % 
   %.

   %>:0 ; 

Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

BackVow    =  Я Ю Ё А О Ы У
              я ю ё а о ы у ;

FrontVow   =  Э Е И Ө Ү 
              э е и ө ү ; 

RealVow = А О Ы У Э Е И Ө Ү
          а о ы у э е и ө ү ;

Cns        =  Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
              б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ъ ь ; 

RealCns        =  Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ 
                  б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ; 


Liquid     = Л 
             л ; 

Nasals      = Н М Ң
              н м ң ; 

Velars         = Ң Г К 
                ң г к ;

VoicelessCns = к п с т ц ш 
               К П С Т Ц Ш ; 

VoicelessCnsRus = З Б В Д Ж Г
                  з б в д ж г ;


!Abstract    = %{z%} %{n%} ;
!FIXME: what's this for?
!FIXME: Usually Abstract is for archiphonemes that never surface, but trigger phonology, such as 1%{и%}%{р%}

Rules


"Remove stem epenthetic vowel in nominative"
%{и%}:0 <=> _ .#. ; 

"Harmony of vowel in some stems"
%{и%}:Vy <=> [ :Vx :Cns* :Cns ] _ (%{☭%}:) %>: ;
         except 
             с к _ (%{☭%}:) %>: ;
         where Vx in ( ү и е э ө а о ы у я ё ю )
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"Remove stem epenthetic vowel in nominative"
%{и%}:и <=> с к _ (%{☭%}:) %>: ; 


!@ Б е л о ц а р с к {и}:и >:0 {D}:д {A}:е н

!@ п р е з и д е н т {и}:и >:0 {z}:з {I}:и {n}:0

!@ ф о н д {и}:у {☭}:0 >:0 {z}:з {I}:у {n}:0

!"D nasal harmony"
!%{D%}:н <=> :Nasals/[ :0 - Abstract:0 ] _ :Vow :Nasals ;
!        except 
!            %{n%}: %>: _ ; 

!"Deletion of {z} after a consonant"
!%{z%}:0 <=> [ :Cns (:ь) | й:0 ]/[ %>: | [ :0 - й:0 ] ] _ ;

!!== Lenition ==!!

"{z} surfaces after a vowel"
%{z%}:з <=> :Vow/:0* _ ;
        except
            [ :Vow Velars: ]/:0* _ ;
            г:/:0* _ ;

!@ с о ң:0 >:0 {z}:0 {I}:о {n}:н >:0 {D}:д {A}:а

"Intervocalic voicing at a morpheme boundary"
Cx:Cy <=> :Vow/:0 _ %>: :Vow/:0 ;
          р/:0 _ %>: :Vow ;
!except??          :VoicelessCns/:0* _ ;
      where Cx in ( п т с ш )
            Cy in ( в д з ж )
      matched ;

!@ к а с:з >:0 {z}:0 {I}:ы

!@ к и р {y}:0 ш:ж >:0 {i}:и п 

!@ к о н к у р с:з >:0 {z}:0 {I}:у {n}:н >:0 {G}:г {A}:а

!@ ч у р т >:0 {z}:0 {I}:у {n}:0       

"Intervocalic voicing of к at morpheme boundary in monosyllabic stems"
к:г <=> .#. [ ( :Cns* ) ( :Vow* ) :Vow ]/:0 _ [ %>: :Vow ]/[ :0 - %>: ]* ;
                                       :Vow _ %{☭%}: %>: (:0) :Vow ; 

!@ с о о к:г >:0 {z}:0 {I}:у {n}:0

!@ р ы н о к:г {☭}:0 >:0 {z}:0 {I}:у {n}:0

"Intervocalic voiced velar deletion"
Cx:0 <=> :Vow/:0* _ [ %>: :Vow ]/:0* ;
     except
         :Vow _ %{☭%}: %>: (:0) :Vow ; 
     where Cx in ( г ң ) ;

"Intervocalic voiceless velar deletion"
к:0 <=> :Vow/:0* _ [ %>: :Vow ]/:0* ;
     except
         .#. [ ( :Cns* ) ( :Vow* ) :Vow ]/:0 _ [ %>: :Vow ]/[ :0 - %>: ]* ;
         :Vow _ %{☭%}: %>: (:0) :Vow ; 


!@ ө к:г >:0 {i}:ү м
!@ ө г:0 >:0 {i}:ө м
!@ б е л е:э к:0 >:0 {i}:э м
!@ б и л и:и к:0 >:0 {i}:и м

"Intervocalic {G} deletion"
%{g%}:0 <=> :Vow/:0* _ ;


!!!== Desonorisation ==!!!

"{L} desonorisation"
%{L%}:д <=> :Liquid/[ ь | :0]* _ ;

!@ м е д а л ь >:0 {L}:д {A}:а р

! {L} desonorisation should *not* happen here:
!@ д а г >:0 {L}:л {A}:а р

"N Desonorisation"
%{N%}:д <=> :Liquid/[ ь | :0]* _ ;


"{L} nasal assimilation"
%{L%}:н <=> :Nasals %>: _ ;

"л to н assimilation when no epenthetic vowel"
л:н <=> _ %{y%}:0 н ;


"Deletion of {n} word final"
%{n%}:0 <=>  _ (:0) .#. ;
             _ %>: %{N%}: ; 

!@ б ү р ү >:0 {z}:з {I}:ү {n}:0

"Forward voicing assimilation"
Cx:Cy <=> :VoicelessCns/[ %>: | %  | %{☭%}:0 | %{ъ%}:0 ]* _ ;
                               VoicelessCnsRus %{☭%}: %>: _ ;
!          AbstrVoicelessCns:/[ %-: | %>: ] _ ;
          where Cx in ( %{M%}  %{D%}  %{L%}  %{N%}  %{G%} %{B%} )
                Cy in (   п      т      т      т      к     п   )
                matched;

!@ с а д {☭}:0 >:0 {D}:т {A}:а

!"Forward voicing assimilation of normal stuff"
!Cx:Cy <=> :VoicelessCns/:0* _ ;
!          where Cx in ( л н )
!                Cy in ( т т )
!                matched;

"{B} nasal assimilation"
%{B%}:м <=> :Nasals %>: _ ;

"{B} after vowel "
%{B%}:в <=> :Vow %>: _ ;


"{g} voicing assimilation"
%{g%}:к <=> :VoicelessCns/:0* _ ;


!!!== Vowel harmony ==!!!

"Harmony of epenthesised vowel in some stems"
%{y%}:Vy <=> [ :Vx :Cns* :Cns ]/[ %>: | :0 ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>: ] ;
         where Vx in ( ү и е э ө а о ы у я ё ю )
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"{i} harmony"
%{i%}:Vy <=> :Vx [ :Cns* :RealCns ]/:0* _ ;
         except
             [ :Vx Velars:0 ]/[ :0 - Velars: ]* _ ;
         !    :Vow/:0* _ ;
         where Vx in ( ү и е э ө а о ы у я ё ю )
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"{i} deletion after vowel"
%{i%}:0 <=> :Vow/:0* _ ;
        except
            [ :Vow Velars:0 ]/[ :0 - Velars: ]* _ ;

"Normal front vowel harmony for {A}"
%{A%}:е <=> [ %{э%}: | :FrontVow ] [ :Cns* :RealCns ]/[:0* | ь ] _ ;
!            Vow: [ %>: %{G%}:0 ]/:0* _ ; ! for dative case where {G} deletes
            [ FrontVow: й: ]/:0* _ ;  !@ ө й:0 >:0 {A}:е р
        except
            [ :FrontVow %{g%}:0 ]/[ :0 - %{g%}: ]* _ ;

!@ Т ы в а   А С С Р {э}:0 >:0 {G}:г {A}:е

"yotted {A} harmony"
%{A%}:я <=> [ BackVow: й: ]/:0* _ ;
         except
             Vow: [ %>: %{G%}:0 ]/:0* _ ; 

"{I} harmony"
%{I%}:Vy <=> :Vx [ :Cns* :RealCns ]/:0* _ ;
             .#. (RealCns:) :Vx RealCns: %>: _ ; ! Monosyllabic stems with cns deletion
         !!    :Vx/:0* _ ;
         except
             [ :Vx Velars:0 ]/[ :0 - Velars: ]* _ ;
         !    :Vow/:0* _ ;
             %{n%}:0 %>: %{N%}:н _ .#. ;
         !!    [ :Vow :Vow ]/:0* _ ;
             [ :Vx р:0 ]/[ :0 - р: ]* _ ;
             [ :Vx й:0 ]/[ :0 - й: ]* _ ;
         where Vx in ( ү и е э ө а о ы у я ё ю )
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"{I} harmony immediately after short vowels"
%{I%}:Vy <=> :Vx/:0* _ ;
         except
             [ :Vow :RealVow ]/:0* _ ;
             [ :Vx Velars:0 ]/[ :0 - Velars: ]* _ ;
         where Vx in ( ү и е э ө а о ы у я ё ю ) ! FIXME: CHECK о ө ё
               Vy in ( ү и э э ө а у ы у а у у )
         matched ;

"{I} deletion after long vowels"
%{I%}:0 <=> [ :Vow :RealVow ]/:0* _ ;
        except
            [ :Vow Velars:0 ]/[ :0 - Velars: ]* _ ;

!@ Т ы в а   А С С Р {э}:0 >:0 {N}:н {I}:и ң

!@ ч у г:0 >:0 %{I}:у р

!@ х ү н >:0 {z}:0 {I}:ү {n}:0


"Turn {E} into й after a vowel"
%{E%}:й <=> [ :Vow ]/[ %>: | :0 ] _ ;


!!== Vowel harmony in context of other processes ==!!

"{I} harmony after deleted velar"
%{I%}:Vy <=> [ Vx: Velars:0 ]/[ :0 - Velars: ]* _ ;
         where Vx in ( и ү е ө а о ы у я )
               Vy in ( и ү э ө а о ы у а )
         matched ;

"{i} harmony after deleted velar"
%{i%}:Vy <=> [ Vx: Velars:0 ]/[ :0 - Velars: ]* _ ;
         where Vx in ( и ү е ө а о ы у я )
               Vy in ( и ү э ө а о ы у а )
         matched ;

"(verb)stem vowel-final assimliation before {g} deletion"
Vx:Vy <=> _ %>: %{g%}:0 %{A%}: ;
      where Vx in ( и е у ү ) ! а э  FIXME: are there other vowels a verb can dend with, besides those that don't change?
            Vy in ( э э а э ) ! а э
      matched ;

!!FIXME: might be able to combine with above rule?
!! wait for rules to grow some
!"(verb)stem vowel-final assimliation before velar deletion"
!Vx:Vy <=> _ Velars:0 %>: [ %{I%}: | %{i%}: ]/:0* ;
!      where Vx in ( и е а у )
!            Vy in ( и э а у )
!      matched ;

"Fix е to э when it becomes first part of long vowel, e.g. in velar deletion"
е:э <=> _ %{i%}:э/:0 ;
        _ %{I%}:э/:0 ;
!        _ %{A%}:э/:0 ;

!@ н е г е:э >:0 {g}:0 {A}:э н

"Vowel harmony for {A} after deleted {g}"
%{A%}:э <=> [ :FrontVow %{g%}:0 ]/[ :0 - %{g%}: ]* _ ;

!@ б и ж и:э >:0 {g}:0 {A}:э н

!"{A} stays а" ! FIXME
!%{A%}:а <=> :BackVow/[ :0 - й: ]* _ ;



!!== Morphological stuff ==!!

"{I} deletion in px3.acc"
%{I%}:0 <=> %{n%}:0 %>: %{N%}:н _ .#. ;


!!== Misc segmental stuff ==!!

"/рк/ cluster to [рт]"
%{к%}:т <=> :р _ .#. ;
        :р _ %>: :Cns/:0* ;

"{г} devoicing when syllable-final"
%{г%}:к <=> :Vow _ [ :VoicelessCns %>: :Vow ]/:0* ;

"{г} surfaces as г syllable finally before some(?) consonants"
%{г%}:г <=> :Vow _ [ :Liquid %>: :Vow ]/:0* ;

!@ о {г}:г л >:0 {i}:у м

"{г} turns into preceding vowel when in closed syllable"
%{г%}:Vy <=> :Vx _ :Cns/:0* .#. ;
         :Vx _ [ :Cns %>: :Cns ]/:0* ;
     except
         !:Vow _ [ :VoicelessCns %>: :Vow ]/:0* ; ! needed to avoid conflicts, meh
         :Vow _ [ :Cns %>: :Vow ]/:0* ;
     where Vx in ( а о )
           Vy in ( а о )
     matched ;

"{н} surfaces as н when syllable-finally"
%{н%}:н <=> :Vow _ [ :Cns :Vow ]/:0* ;

!"{н} deletion when part of coda cluster"
!%{н%}:0 <=> :Vow _ [ :Cns :Cns ]/:0* ;
!            :Vow _ :Cns/:0* .#. ;

"ш to ч after {н} when it surfaces"
ш:ч <=> %{н%}:н _ ;

"ъ deletion syllable finally"
ъ:0 <=> :Vow _ [ :Cns :Vow ]/:0* ;

"ь to и before px3sp"
ь:0 <=> _ %>: %{z%}:  ; 

!@ л а г е р ь:0 >:0 {z}:0 {I}:и {n}:н >:0 {G}:г {A}:е 

"р deletion in aorist"
р:0 <=> _ [ %>: %{I%}: ]/:0* р ;

!@ б а р:0 >:0 {I}:а р

"й deletion before yot vowels"
й:0 <=> _ %{A%}:/:0* ;

!@ ч а й:0 >:0 {A}:я р


!"г deletion in aorist"
!г:0 <=> _ %>: [ %{A%}: | %{I%}: ] р ; 

!"т deletion in -ст consonant cluster"
!т:0 <=> .#. [ Vow: | Cns: ]* с _ %>: %{L%}: %{A%}: ; 

!@ а р т и с т:0 >:0 {L}:т {A}:е р

!@ ч а ъ:0 с:з >:0 {i}:ы м
!@ ч а ъ:0 с:з >:0 {i}:ы м >:0 {G}:г {A}:а


!@ ч е ч е к >:0 {L}:т {A}:е р >:0 {D}:д {A}:е н
!@ ө ө к:г >:0 {z}:0 {I}:ү {n}:0 
!@ ш а к:г >:0 {z}:0 {I}:ы {n}:0 
!@ о т >:0 {D}:т {A}:а н
!@ к и ж и >:0 {D}:д {A}:е н
!@ х ө й л е ң >:0 {D}:д {A}:е н
!@ х ө й л е ң >:0 {N}:н {I}:и ң

!@ а й т ы р ы г:0 >:0 {z}:0 {I}:ы {n}:0
