
Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
   а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я

   %{I%}:ы %{I%}:и %{I%}:у %{I%}:ү

   %{A%}:а %{A%}:е

   %{D%}:д 
 
   %{L%}:л %{L%}:н %{L%}:т %{L%}:д

   %{N%}:н 
 
   %{G%}:г %{G%}:к
 
   %{J%}:ж %{J%}:ч

   %{B%}:б

   %{i%}:0 %{i%}:ы %{i%}:и %{i%}:у %{i%}:ү

   %{z%}:з %{n%}:н %{y%}:ы
   %{z%}:0 %{n%}:0 %{y%}:0

   %{и%}:и

   %{☭%}:0

   %>:0 ; 

Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

BackVow    =  Я Ю Ё А О Ы У
              я ю ё а о ы у ;

FrontVow   =  Э Е И Ө Ү 
              э е и ө ү ; 

Cns        =  Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
              б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ъ ь ; 

Liquid     = Л 
             л ; 

Nasals      = Н М Ң
              н м ң ; 

NNg         = Н Ң
              н ң ;

VoicelessCns = к п с т ц ш 
               К П С Т Ц Ш ; 

VoicelessCnsRus = б в д ж г ;


Abstract    = %{z%} %{n%} ;

Rules


"Remove stem epenthetic vowel in nominative"
%{и%}:0 <=> _ .#. ; 

"N Desonorisation"
%{N%}:д <=> [ :Liquid ] [ %>: ]* _ ;

"D nasal harmony"
%{D%}:н <=> :Nasals/[ :0 - Abstract:0 ] _ :Vow :Nasals ;
        except 
            %{n%}: %>: _ ; 

!"Deletion of {z} after a consonant"
!%{z%}:0 <=> [ :Cns (:ь) | й:0 ]/[ %>: | [ :0 - й:0 ] ] _ ;

!!== Lenition ==!!

"{z} surfaces after a vowel"
%{z%}:з <=> :Vow/:0* _ ;
        except
            [ :Vow NNg: ]/:0* _ ;
            г:/:0* _ ;

!@ с о ң:0 >:0 {z}:0 {I}:о {n}:н >:0 {D}:д {A}:а

"Intervocalic voicing at a morpheme boundary"
Cx:Cy <=> :Vow/:0 _ %>: :Vow/:0 ;
      where Cx in ( п т с ш к )
            Cy in ( б д з ж г )
      matched ;

!@ с о о к:г >:0 {z}:0 {I}:у

"{L} after vowel"
%{L%}:л <=> :Vow/:0* %>: _ ;

"{L} nasal assimilation"
%{L%}:н <=> :Nasals %>: _ ;

"Deletion of {n} word final"
%{n%}:0 <=>  _ (:0) .#. ;
             _ %>: %{N%}: ; 

!@ б ү р ү >:0 {z}:з {I}:ү {n}:0

"Intervocalic nasal deletion"
Cx:0 <=> :Vow/:0* _ [ %>: :Vow ]/:0* ;
     where Cx in NNg ;

"Forward voicing assimilation"
Cx:Cy <=> :VoicelessCns/[ %>: | %  | %{☭%}:0 | %{ъ%}:0 ] _ ;
!          AbstrVoicelessCns:/[ %-: | %>: ] _ ;
          where Cx in ( %{M%}  %{D%}  %{L%}  %{N%} )
                Cy in (   п      т      т      т   )
                matched;

"Epenthesis in some stems"
%{y%}:0 <=> _ [ :Cns :Vow ]/[ :0 | %>: ] ;

"Harmony of unepenthesised vowel in some stems"
%{y%}:и <=> [ :FrontVow :Cns* :Cns ]/[ %>: | :0 ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>: ] ;

"{i} harmony after deleted nasal"
%{i%}:Vy <=> [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         where Vx in ( а о ө я )
               Vy in ( а о ө а )
         matched ;

"{I} harmony after deleted nasal"
%{I%}:Vy <=> [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         where Vx in ( а о ө я )
               Vy in ( а о ө а )
         matched ;

"{I} deletion in px3.acc"
%{I%}:0 <=> %{n%}:0 %>: %{N%}:н _ .#. ;



!!== Misc segmental stuff ==!!

"/рк/ cluster to [рт]"
к:т <=> :р _ .#. ;
        :р _ :Cns/:0* ;

"г devoicing when syllable-final"
г:к <=> :Vow _ [ :VoicelessCns :Vow ]/:0* ;

"г turns into preceding vowel when in closed syllable"
г:Vy <=> :Vx _ :Cns/:0* .#. ;
         :Vx _ [ :Cns :Cns ]/:0* ;
     except
         :Vow _ [ :VoicelessCns :Vow ]/:0* ; ! was causing conflicts, meh
     where Vx in ( а о )
           Vy in ( а о )
     matched ;

"ъ deletion syllable finally"
ъ:0 <=> :Vow _ [ :Cns :Vow ]/:0* ;
