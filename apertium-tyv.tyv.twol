
Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
   а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я

   %{I%}:ы ! %{I%}:и %{I%}:у %{I%}:ү

   %{A%}:а ! %{A%}:е

   %{D%}:д 
 
   %{L%}:л 

   %{N%}:н 
 
   %{G%}:г %{G%}:к
 
   %{J%}:ж %{J%}:ч

   %{B%}:б

   %{i%}:0 ! %{i%}:ы %{i%}:и %{i%}:у %{i%}:ү

   %{z%}:з %{n%}:н %{y%}:ы
   %{z%}:0 %{n%}:0 %{y%}:0

   %{и%}:и

   %{к%}:к
   %{г%}:г

   %{☭%}:0

   %.

   %>:0 ; 

Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

BackVow    =  Я Ю Ё А О Ы У
              я ю ё а о ы у ;

FrontVow   =  Э Е И Ө Ү 
              э е и ө ү ; 

Cns        =  Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
              б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ъ ь ; 

RealCns        =  Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ 
                  б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ; 


Liquid     = Л 
             л ; 

Nasals      = Н М Ң
              н м ң ; 

NNg         = Н Ң
              н ң ;

VoicelessCns = к п с т ц ш 
               К П С Т Ц Ш ; 

VoicelessCnsRus = б в д ж г ;


Abstract    = %{z%} %{n%} ;

Rules


"Remove stem epenthetic vowel in nominative"
%{и%}:0 <=> _ .#. ; 

!"D nasal harmony"
!%{D%}:н <=> :Nasals/[ :0 - Abstract:0 ] _ :Vow :Nasals ;
!        except 
!            %{n%}: %>: _ ; 

!"Deletion of {z} after a consonant"
!%{z%}:0 <=> [ :Cns (:ь) | й:0 ]/[ %>: | [ :0 - й:0 ] ] _ ;

!!== Lenition ==!!

"{z} surfaces after a vowel"
%{z%}:з <=> :Vow/:0* _ ;
        except
            [ :Vow NNg: ]/:0* _ ;
            г:/:0* _ ;

!@ с о ң:0 >:0 {z}:0 {I}:о {n}:н >:0 {D}:д {A}:а

"Intervocalic voicing at a morpheme boundary"
Cx:Cy <=> :Vow/:0 _ %>: :Vow/:0 ;
      where Cx in ( п т с ш к )
            Cy in ( б д з ж г )
      matched ;

!@ с о о к:г >:0 {z}:0 {I}:у



"Intervocalic nasal deletion"
Cx:0 <=> .#. :Vow/:0* _ [ %>: :Vow ]/:0* ;
     where Cx in NNg ;



!!!== Desonorisation ==!!!

"{L} desonorisation"
%{L%}:д <=> :Liquid/:0* _ ;

! {L} desonorisation should *not* happen here:
!@ д а г >:0 {L}:л {A}:а р

"N Desonorisation"
%{N%}:д <=> :Liquid/:0* _ ;


"{L} nasal assimilation"
%{L%}:н <=> :Nasals %>: _ ;

"Deletion of {n} word final"
%{n%}:0 <=>  _ (:0) .#. ;
             _ %>: %{N%}: ; 

!@ б ү р ү >:0 {z}:з {I}:ү {n}:0

"Forward voicing assimilation"
Cx:Cy <=> :VoicelessCns/[ %>: | %  | %{☭%}:0 | %{ъ%}:0 ] _ ;
                              VoicelessCnsRus %{☭%}: %>: _ ; 
!          AbstrVoicelessCns:/[ %-: | %>: ] _ ;
          where Cx in ( %{M%}  %{D%}  %{L%}  %{N%} )
                Cy in (   п      т      т      т   )
                matched;

!@ с а д {☭}:0 >:0 {D}:т {A}:а

"Epenthesis in some stems"
%{y%}:0 <=> _ [ :Cns :Vow ]/[ :0 | %>: ] ;

"Harmony of unepenthesised vowel in some stems"
%{y%}:Vy <=> [ :Vx :Cns* :Cns ]/[ %>: | :0 ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>: ] ;
         where Vx in ( ү и е э ө а о ы у я ё ю ) ! Check и
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"{i} harmony after deleted nasal"
%{i%}:Vy <=> [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         where Vx in ( а о ө я )
               Vy in ( а о ө а )
         matched ;

"{i} harmony"
%{i%}:Vy <=> :Vx [ :Cns* :RealCns ]/:0* _ ;
         except
             [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         !    :Vow/:0* _ ;
         where Vx in ( ү и е э ө а о ы у я ё ю ) ! Check и
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

"{i} deletion after vowel"
%{i%}:0 <=> :Vow/:0* _ ;
        except
            [ :Vow NNg:0 ]/[ :0 - NNg: ]* _ ;

"Vowel harmony for {A}"
%{A%}:е <=> :FrontVow [ :Cns* :RealCns ]/[:0* | ь ] _ ;
            Vow: [ %>: %{G%}:0 ]/:0* _ ; ! for dative case where {G} deletes

"{I} harmony after deleted nasal"
%{I%}:Vy <=> [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         where Vx in ( а о ө я )
               Vy in ( а о ө а )
         matched ;

"{I} harmony"
%{I%}:Vy <=> :Vx [ :Cns* :RealCns ]/:0* _ ;
             .#. (RealCns:) :Vx RealCns: %>: _ ; ! Monosyllabic stems with cns deletion
         except
             [ :Vx NNg:0 ]/[ :0 - NNg: ]* _ ;
         !    :Vow/:0* _ ;
             %{n%}:0 %>: %{N%}:н _ .#. ;
         where Vx in ( ү и е э ө а о ы у я ё ю ) ! Check и
               Vy in ( ү и и и ү ы у ы у ы у у )
         matched ;

!@ ч у г:0 >:0 %{I}:у р

!@ х ү н >:0 {z}:0 {I}:ү {n}:0

"{I} deletion in px3.acc"
%{I%}:0 <=> %{n%}:0 %>: %{N%}:н _ .#. ;

!!== Misc segmental stuff ==!!

"/рк/ cluster to [рт]"
%{к%}:т <=> :р _ .#. ;
        :р _ %>: :Cns/:0* ;

"{г} devoicing when syllable-final"
%{г%}:к <=> :Vow _ [ :VoicelessCns %>: :Vow ]/:0* ;

"{г} surfaces as г syllable finally before some(?) consonants"
%{г%}:г <=> :Vow _ [ :Liquid %>: :Vow ]/:0* ;

!@ о {г}:г л >:0 {i}:у м

"{г} turns into preceding vowel when in closed syllable"
%{г%}:Vy <=> :Vx _ :Cns/:0* .#. ;
         :Vx _ [ :Cns %>: :Cns ]/:0* ;
     except
         !:Vow _ [ :VoicelessCns %>: :Vow ]/:0* ; ! needed to avoid conflicts, meh
         :Vow _ [ :Cns %>: :Vow ]/:0* ;
     where Vx in ( а о )
           Vy in ( а о )
     matched ;

"ъ deletion syllable finally"
ъ:0 <=> :Vow _ [ :Cns :Vow ]/:0* ;

"г deletion in aorist"
г:0 <=> _ %>: [ %{A%}: | %{I%}: ] р ; 


!@ ч а ъ:0 с:з >:0 {i}:ы м
!@ ч а ъ:0 с:з >:0 {i}:ы м >:0 {G}:г {A}:а


!@ ч у р т >:0 {z}:0 {I}:у {n}:0       
!@ ч е ч е к >:0 {L}:т {A}:е р >:0 {D}:д {A}:е н
!@ ө ө к:г >:0 {z}:0 {I}:ү {n}:0 
!@ ш а к:г >:0 {z}:0 {I}:ы {n}:0 
!@ о т >:0 {D}:т {A}:а н
!@ к и ж и >:0 {D}:д {A}:е н
!@ х ө й л е ң >:0 {D}:д {A}:е н
!@ х ө й л е ң >:0 {N}:н {I}:и ң

